link:
reference/lexical_analysis.html#literals

docs:

2.4. Literals¶
Literals are notations for constant values of some built-in types.

2.4.1. String and Bytes literals¶
String literals are described by the following lexical definitions:

stringliteral   ::=  [stringprefix](shortstring | longstring)
stringprefix    ::=  "r" | "u" | "R" | "U" | "f" | "F"
                     | "fr" | "Fr" | "fR" | "FR" | "rf" | "rF" | "Rf" | "RF"
shortstring     ::=  "'" shortstringitem* "'" | '"' shortstringitem* '"'
longstring      ::=  "'''" longstringitem* "'''" | '"""' longstringitem* '"""'
shortstringitem ::=  shortstringchar | stringescapeseq
longstringitem  ::=  longstringchar | stringescapeseq
shortstringchar ::=  <any source character except "\" or newline or the quote>
longstringchar  ::=  <any source character except "\">
stringescapeseq ::=  "\" <any source character>


bytesliteral   ::=  bytesprefix(shortbytes | longbytes)
bytesprefix    ::=  "b" | "B" | "br" | "Br" | "bR" | "BR" | "rb" | "rB" | "Rb" | "RB"
shortbytes     ::=  "'" shortbytesitem* "'" | '"' shortbytesitem* '"'
longbytes      ::=  "'''" longbytesitem* "'''" | '"""' longbytesitem* '"""'
shortbytesitem ::=  shortbyteschar | bytesescapeseq
longbytesitem  ::=  longbyteschar | bytesescapeseq
shortbyteschar ::=  <any ASCII character except "\" or newline or the quote>
longbyteschar  ::=  <any ASCII character except "\">
bytesescapeseq ::=  "\" <any ASCII character>

One syntactic restriction not indicated by these productions is that whitespace
is not allowed between the stringprefix or bytesprefix and the
rest of the literal. The source character set is defined by the encoding
declaration; it is UTF-8 if no encoding declaration is given in the source file;
see section Encoding declarations.
In plain English: Both types of literals can be enclosed in matching single quotes
(') or double quotes (").  They can also be enclosed in matching groups
of three single or double quotes (these are generally referred to as
triple-quoted strings).  The backslash (\) character is used to escape
characters that otherwise have a special meaning, such as newline, backslash
itself, or the quote character.
Bytes literals are always prefixed with 'b' or 'B'; they produce an
instance of the bytes type instead of the str type.  They
may only contain ASCII characters; bytes with a numeric value of 128 or greater
must be expressed with escapes.
Both string and bytes literals may optionally be prefixed with a letter 'r'
or 'R'; such strings are called raw strings and treat backslashes as
literal characters.  As a result, in string literals, '\U' and '\u'
escapes in raw strings are not treated specially. Given that Python 2.x’s raw
unicode literals behave differently than Python 3.x’s the 'ur' syntax
is not supported.

New in version 3.3: The 'rb' prefix of raw bytes literals has been added as a synonym
of 'br'.


New in version 3.3: Support for the unicode legacy literal (u'value') was reintroduced
to simplify the maintenance of dual Python 2.x and 3.x codebases.
See PEP 414 for more information.

A string literal with 'f' or 'F' in its prefix is a
formatted string literal; see Formatted string literals.  The 'f' may be
combined with 'r', but not with 'b' or 'u', therefore raw
formatted strings are possible, but formatted bytes literals are not.
In triple-quoted literals, unescaped newlines and quotes are allowed (and are
retained), except that three unescaped quotes in a row terminate the literal.  (A
“quote” is the character used to open the literal, i.e. either ' or ".)
Unless an 'r' or 'R' prefix is present, escape sequences in string and
bytes literals are interpreted according to rules similar to those used by
Standard C.  The recognized escape sequences are:







Escape Sequence
Meaning
Notes



\newline
Backslash and newline ignored
 

\\
Backslash (\)
 

\'
Single quote (')
 

\"
Double quote (")
 

\a
ASCII Bell (BEL)
 

\b
ASCII Backspace (BS)
 

\f
ASCII Formfeed (FF)
 

\n
ASCII Linefeed (LF)
 

\r
ASCII Carriage Return (CR)
 

\t
ASCII Horizontal Tab (TAB)
 

\v
ASCII Vertical Tab (VT)
 

\ooo
Character with octal value
ooo
(1,3)

\xhh
Character with hex value hh
(2,3)



Escape sequences only recognized in string literals are:







Escape Sequence
Meaning
Notes



\N{name}
Character named name in the
Unicode database
(4)

\uxxxx
Character with 16-bit hex value
xxxx
(5)

\Uxxxxxxxx
Character with 32-bit hex value
xxxxxxxx
(6)



Notes:

As in Standard C, up to three octal digits are accepted.

Unlike in Standard C, exactly two hex digits are required.

In a bytes literal, hexadecimal and octal escapes denote the byte with the
given value. In a string literal, these escapes denote a Unicode character
with the given value.


Changed in version 3.3: Support for name aliases [1] has been added.


Exactly four hex digits are required.

Any Unicode character can be encoded this way.  Exactly eight hex digits
are required.


Unlike Standard C, all unrecognized escape sequences are left in the string
unchanged, i.e., the backslash is left in the result.  (This behavior is
useful when debugging: if an escape sequence is mistyped, the resulting output
is more easily recognized as broken.)  It is also important to note that the
escape sequences only recognized in string literals fall into the category of
unrecognized escapes for bytes literals.


Changed in version 3.6: Unrecognized escape sequences produce a DeprecationWarning.  In
some future version of Python they will be a SyntaxError.


Even in a raw literal, quotes can be escaped with a backslash, but the
backslash remains in the result; for example, r"\"" is a valid string
literal consisting of two characters: a backslash and a double quote; r"\"
is not a valid string literal (even a raw string cannot end in an odd number of
backslashes).  Specifically, a raw literal cannot end in a single backslash
(since the backslash would escape the following quote character).  Note also
that a single backslash followed by a newline is interpreted as those two
characters as part of the literal, not as a line continuation.


2.4.2. String literal concatenation¶
Multiple adjacent string or bytes literals (delimited by whitespace), possibly
using different quoting conventions, are allowed, and their meaning is the same
as their concatenation.  Thus, "hello" 'world' is equivalent to
"helloworld".  This feature can be used to reduce the number of backslashes
needed, to split long strings conveniently across long lines, or even to add
comments to parts of strings, for example:
re.compile("[A-Za-z_]"       # letter or underscore
           "[A-Za-z0-9_]*"   # letter, digit or underscore
          )


Note that this feature is defined at the syntactical level, but implemented at
compile time.  The ‘+’ operator must be used to concatenate string expressions
at run time.  Also note that literal concatenation can use different quoting
styles for each component (even mixing raw strings and triple quoted strings),
and formatted string literals may be concatenated with plain string literals.


2.4.3. Formatted string literals¶

New in version 3.6.

A formatted string literal or f-string is a string literal
that is prefixed with 'f' or 'F'.  These strings may contain
replacement fields, which are expressions delimited by curly braces {}.
While other string literals always have a constant value, formatted strings
are really expressions evaluated at run time.
Escape sequences are decoded like in ordinary string literals (except when
a literal is also marked as a raw string).  After decoding, the grammar
for the contents of the string is:

f_string          ::=  (literal_char | "{{" | "}}" | replacement_field)*
replacement_field ::=  "{" f_expression ["!" conversion] [":" format_spec] "}"
f_expression      ::=  (conditional_expression | "*" or_expr)
                         ("," conditional_expression | "," "*" or_expr)* [","]
                       | yield_expression
conversion        ::=  "s" | "r" | "a"
format_spec       ::=  (literal_char | NULL | replacement_field)*
literal_char      ::=  <any code point except "{", "}" or NULL>

The parts of the string outside curly braces are treated literally,
except that any doubled curly braces '{{' or '}}' are replaced
with the corresponding single curly brace.  A single opening curly
bracket '{' marks a replacement field, which starts with a
Python expression.  After the expression, there may be a conversion field,
introduced by an exclamation point '!'.  A format specifier may also
be appended, introduced by a colon ':'.  A replacement field ends
with a closing curly bracket '}'.
Expressions in formatted string literals are treated like regular
Python expressions surrounded by parentheses, with a few exceptions.
An empty expression is not allowed, and a lambda expression
must be surrounded by explicit parentheses.  Replacement expressions
can contain line breaks (e.g. in triple-quoted strings), but they
cannot contain comments.  Each expression is evaluated in the context
where the formatted string literal appears, in order from left to right.
If a conversion is specified, the result of evaluating the expression
is converted before formatting.  Conversion '!s' calls str() on
the result, '!r' calls repr(), and '!a' calls ascii().
The result is then formatted using the format() protocol.  The
format specifier is passed to the __format__() method of the
expression or conversion result.  An empty string is passed when the
format specifier is omitted.  The formatted result is then included in
the final value of the whole string.
Top-level format specifiers may include nested replacement fields. These nested
fields may include their own conversion fields and format specifiers, but may not include more deeply-nested replacement fields. The
format specifier mini-language is the same as that used by
the string .format() method.
Formatted string literals may be concatenated, but replacement fields
cannot be split across literals.
Some examples of formatted string literals:
>>> name = "Fred"
>>> f"He said his name is {name!r}."
"He said his name is 'Fred'."
>>> f"He said his name is {repr(name)}."  # repr() is equivalent to !r
"He said his name is 'Fred'."
>>> width = 10
>>> precision = 4
>>> value = decimal.Decimal("12.34567")
>>> f"result: {value:{width}.{precision}}"  # nested fields
'result:      12.35'
>>> today = datetime(year=2017, month=1, day=27)
>>> f"{today:%B %d, %Y}"  # using date format specifier
'January 27, 2017'
>>> number = 1024
>>> f"{number:#0x}"  # using integer format specifier
'0x400'


A consequence of sharing the same syntax as regular string literals is
that characters in the replacement fields must not conflict with the
quoting used in the outer formatted string literal:
f"abc {a["x"]} def"    # error: outer string literal ended prematurely
f"abc {a['x']} def"    # workaround: use different quoting


Backslashes are not allowed in format expressions and will raise
an error:
f"newline: {ord('\n')}"  # raises SyntaxError


To include a value in which a backslash escape is required, create
a temporary variable.
>>> newline = ord('\n')
>>> f"newline: {newline}"
'newline: 10'


Formatted string literals cannot be used as docstrings, even if they do not
include expressions.
>>> def foo():
...     f"Not a docstring"
...
>>> foo.__doc__ is None
True


See also PEP 498 for the proposal that added formatted string literals,
and str.format(), which uses a related format string mechanism.


2.4.4. Numeric literals¶
There are three types of numeric literals: integers, floating point numbers, and
imaginary numbers.  There are no complex literals (complex numbers can be formed
by adding a real number and an imaginary number).
Note that numeric literals do not include a sign; a phrase like -1 is
actually an expression composed of the unary operator ‘-‘ and the literal
1.


2.4.5. Integer literals¶
Integer literals are described by the following lexical definitions:

integer      ::=  decinteger | bininteger | octinteger | hexinteger
decinteger   ::=  nonzerodigit (["_"] digit)* | "0"+ (["_"] "0")*
bininteger   ::=  "0" ("b" | "B") (["_"] bindigit)+
octinteger   ::=  "0" ("o" | "O") (["_"] octdigit)+
hexinteger   ::=  "0" ("x" | "X") (["_"] hexdigit)+
nonzerodigit ::=  "1"..."9"
digit        ::=  "0"..."9"
bindigit     ::=  "0" | "1"
octdigit     ::=  "0"..."7"
hexdigit     ::=  digit | "a"..."f" | "A"..."F"

There is no limit for the length of integer literals apart from what can be
stored in available memory.
Underscores are ignored for determining the numeric value of the literal.  They
can be used to group digits for enhanced readability.  One underscore can occur
between digits, and after base specifiers like 0x.
Note that leading zeros in a non-zero decimal number are not allowed. This is
for disambiguation with C-style octal literals, which Python used before version
3.0.
Some examples of integer literals:
7     2147483647                        0o177    0b100110111
3     79228162514264337593543950336     0o377    0xdeadbeef
      100_000_000_000                   0b_1110_0101



Changed in version 3.6: Underscores are now allowed for grouping purposes in literals.



2.4.6. Floating point literals¶
Floating point literals are described by the following lexical definitions:

floatnumber   ::=  pointfloat | exponentfloat
pointfloat    ::=  [digitpart] fraction | digitpart "."
exponentfloat ::=  (digitpart | pointfloat) exponent
digitpart     ::=  digit (["_"] digit)*
fraction      ::=  "." digitpart
exponent      ::=  ("e" | "E") ["+" | "-"] digitpart

Note that the integer and exponent parts are always interpreted using radix 10.
For example, 077e010 is legal, and denotes the same number as 77e10. The
allowed range of floating point literals is implementation-dependent.  As in
integer literals, underscores are supported for digit grouping.
Some examples of floating point literals:
3.14    10.    .001    1e100    3.14e-10    0e0    3.14_15_93



Changed in version 3.6: Underscores are now allowed for grouping purposes in literals.



2.4.7. Imaginary literals¶
Imaginary literals are described by the following lexical definitions:

imagnumber ::=  (floatnumber | digitpart) ("j" | "J")

An imaginary literal yields a complex number with a real part of 0.0.  Complex
numbers are represented as a pair of floating point numbers and have the same
restrictions on their range.  To create a complex number with a nonzero real
part, add a floating point number to it, e.g., (3+4j).  Some examples of
imaginary literals:
3.14j   10.j    10j     .001j   1e100j   3.14e-10j   3.14_15_93j



